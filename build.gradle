plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.ghostandry'
version = '1.0.0'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.yaml:snakeyaml:2.2'
    compileOnly 'org.jetbrains:annotations:26.0.2-1'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'libmanager'

            pom {
                name = 'LibManager'
                description = 'Gestione librerie per progetti Minecraft'
                url = 'https://github.com/GhostAndry/LibManager'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'GhostAndry'
                        name = 'Andrea Ronzoni'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/GhostAndry/LibManager.git'
                    url = 'https://github.com/GhostAndry/LibManager'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/GhostAndry/LibManager")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}